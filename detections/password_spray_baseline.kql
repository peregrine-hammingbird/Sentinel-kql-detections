let lookback = 14d;
let recent = 1d;
let threshold = 20;

let baseline =
    SigninLogs
    | where TimeGenerated between (ago(lookback) .. ago(recent))
    | where ResultType != 0
    | summarize BaselineFailures = count() by IPAddress;

let recentActivity =
    SigninLogs
    | where TimeGenerated > ago(recent)
    | where ResultType != 0
    | summarize
        TodayFailures = count(),
        DistinctUsers = dcount(UserPrincipalName)
      by IPAddress;

recentActivity
| join kind=leftouter baseline on IPAddress
| extend BaselineFailures = coalesce(BaselineFailures, 0)
| where TodayFailures > threshold and BaselineFailures < 5
| order by TodayFailures desc
